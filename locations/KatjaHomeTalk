# KatjaHomeTalk
CLOSE ALL
gs 'KatjaHomeDin'
gs 'stat'
'<center><b><font color="maroon"><<$npcName[14]>> <<$npcSurName[14]>></font></b></center>'
'<center><img src="images/qwest/alter/npc/14.jpg"></center>'

if sweat > 0:
	'Katja wrinkles her nose "<<$name>>, you''re all sweaty. Do you want to use my shower?"'

	act 'Have a shower':
		cls
		minut += 15
		gs 'stat'

		if KatjaHorny >= 50:
			'Katja takes you in the bathroom. Blushing, she says, "I think I''ll join you."'

			act 'Wash alone':
				cls
				dynamic $showerdin
				minut += 15
				horny += 1
				hapri = 1
				mop = 1
				sweat = -3

				if frost > 0:frost = 0

				gs 'stat'
				! WD: Correct Image path ~ "images/dush.jpg"
				'<center><img src="images/pics/dush.jpg"></center>'
				'You kick Katja out of the bathroom and close the door. You get into the shower and begin to lather your body with a cloth.'
				'After washing off the soap you wash your hair with shampoo. Then, rinse off the shampoo you are completely clean.'

				act 'Get out of the shower':gt $curloc
			end

			if mesec <= 0:
				act 'Bathe together':
					cls
					dynamic $showerdin
					minut += 15
					horny += 1
					hapri = 1
					mop = 1
					sweat = -3

					if frost > 0:frost = 0

					horny += 10
					KatjaHorny += 10
					KatjaLust += 1
					gs 'stat'
					'<center><img src="images/qwest/alter/katja/shower.jpg"></center>'
					'You and Katja undress and both climb into the shower.'

					if lobok <= 0:
						'Katjas eyes slide over your body lingering a little on your breasts and pussy. Katja moves close to you and you rinse yourself and Katja.'

						act 'further':
							cls
							gs 'stat'
							'<center><img src="images/qwest/alter/katja/shower2.jpg"></center>'
							'Katja gently rubs shampoo into your hair then rinses it out with warm shower jets.'

							if KatjaLust >= 10 and KatjaHorny >= 90:
								'Katja washes your pussy and breasts with soap, gently stroking her palms against you. Her cheeks are flushed and her eyes glistening.'
								dynamic $katja_shower_kiss_home
							else
								'You and Katja wash yourselves.'

								act 'Get out of the shower':gt $curloc
							end
						end
					elseif lobok > 0:
						'Katja eyes slide over your body lingering a little on your breasts and pubic hair. Katja smiles, "<<$name>>, you''ve got too much hair, it''s not fashionable. I''ll shave it off."'

						act 'Get shaved':
							cls
							minut += 5
							lobok = -3
							KatjaHorny += 10
							KatjaLust += 1
							gs 'stat'
							'<center><img src="images/qwest/alter/katja/lobok.jpg"></center>'
							'Katja sat in the bath and you lathers your crotch (and most of your stomach and chest) with shaving gel. She takes a razor and begins to gently shave the hair between your legs.'

							if KatjaLust >= 10 and KatjaHorny >= 90:
								'Katja washes your pussy and breasts with soap, gently stroking her palms against you. Her cheeks are flushed and her eyes glistening.'
								dynamic $katja_shower_kiss_home
							else
								'Katja, having finally shaved all the hair between your legs, washes you off with soap.'

								act 'Get out of the shower':gt $curloc
							end
						end
					end
				end
			end
		elseif KatjaHorny < 50:
			dynamic $showerdin
			minut += 15
			horny += 1
			hapri = 1
			mop = 1
			sweat = -3

			if frost > 0:frost = 0

			gs 'stat'
			'<center><img src="images/pics/dush.jpg"></center>'
			'Katja shows you where the toiletries are and leaves you to have a shower. You get into the shower and begin to lather your body with a cloth.'
			'After washing off the soap you wash your hair with shampoo. Then, rinse off the shampoo you are completely clean.'

			act 'Get out of the shower':gt $curloc
		end
	end
else
	!!not sweaty
	if hour < 20:
		if npcSex[14] = 1 and dom>=KatjaDom:
			act 'Offer to take a walk in the Park':
				dom-=1
				minut += 15
				KatjaHorny += 5
				$ev_name = 'Katja'
				$ev_name_a = 'Katja'
				$ev_name_om = 'Katja'
				dick = 0
				place = 1
				Frend_num = 14
				p_lust = KatjaLust
				p_slut = KatjaSlut
				p_dom = KatjaDom
				p_horny = KatjaHorny
				p_alko = 0
				gt'placer'
			end
		end
		act 'Chat with Katja':
			cls
			KatjaHorny += 1
			!KatjaLust += 1
			!if npcSex[14] = 1:
			minut += 15
			gs 'stat'
			'<center><b><font color="maroon"><<$npcName[14]>> <<$npcSurName[14]>></font></b></center>'
			'<center><img src="images/qwest/alter/npc/14.jpg"></center>'

			if katjaPreg = 0 or knowKatjaPreg >= 1:
				if KatjaHorny < 50:'Katka talks about school lessons, cosmetics, the latest fashion trends and other very important things.'

				if KatjaHorny >= 50:
					dynamic $d_katjaSextalk
					exit
				end
			else
				'Katja biting her lip and without looking you in the eye, finally plucks up the courage and says, "<<$name>>, i am pregnant."'

				act 'Whos is it?':
					cls
					minut += 5
					knowKatjaPreg = 1
					gs 'stat'
					'Dumbfounded, you ask, "Who?" Katja wrinkles her nose, "Santa Claus? Well, it was Voinov. I was with him at the Christmas party at school, we slept together and boom! Jesus, what do I do now?"'

					act 'I have no idea':gt $curloc
				end
			end

			act 'further':gt $curloc
		end

		if KatjaLust > 10:
			katkisrand = rand(0, 5)

			if katkisrand = 0:
				cls
				gs 'stat'
				'<center><b><font color="maroon"><<$npcName[14]>> <<$npcSurName[14]>></font></b></center>'
				'<center><img src="images/qwest/alter/npc/14.jpg"></center>'
				dynamic $d_katjahornykiss
			end
		end
	elseif hour >= 20:
		'Katja says, "Let''s go to the disco."'

		if money >= 25:act 'Go to the disco':money -= 25 & minut += 15 & gt 'gdkin'
	end
end

act 'Say goodbye and go home':gt 'gorodok'

--- KatjaHomeTalk ---------------------------------

