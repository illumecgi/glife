# bass
if $ARGS[0] = 'start':
	CLOSE ALL
	$metka = $ARGS[0]
	$loc = $CURLOC
	$metkaM = $ARGS[0]
	$locM = $CURLOC
	cls
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Pool</font></b></center>'
	'<center><img src="images/etogame/bass.jpg"></center>'
	'Entrance to the pool costs 100 rubles, you must have a swimsuit.'

	act 'Use changing room':gt 'changingroom', 'view_swim_list'

	if db >= 1 and $clothingworntype = 'swimwear' and Goraday ! daystart:act 'Search ZHoru':Goraday = daystart & gt 'klofdomhouse', 'ev42'

	if money >= 100 and $clothingworntype = 'swimwear':
		act 'Pay to swim in the pool for an hour (100 rubles)':
			cla
			minut += 5
			money -= 100
			gt 'bass', 'a'
		end
	end
	act'Leave':
		if $clothingworntype = 'swimwear':
			cla
			'You need to get dressed in regular clothing first.'
			act'Return':gt'bass', 'start'
		else
			minut += 5
			gt 'down'
		end
	end
end

if $ARGS[0] = 'a':
	cla
	*clr
	minut += 5
	'<center><b><font color="maroon">Pool</font></b></center>'

	if magik < 4:'<center><img src="images/etogame/bass.jpg"></center>'

	if magik >= 4 and kisameQW = 0:
		'<center><img src="images/etogame/bass1.jpg"></center>'
		'In pool floats hefty athlete, he swims with mad speed and clearly feels like a fish in water, he come to the shore to the edge of the pool and in one motion jumps right in front of you. Looking at you, he greedily smiling showing you a range of white teeth.'

		act 'Watch for swimmers':
			cla
			*clr
			kisameQW = 1
			minut += 60
			sweat = -3
			manna += 5
			willpower += 5

			if stren < 30:stren += 1

			'<center><img src="images/qwest/kisame.jpg"></center>'
			'Athlete put on a towel and walked up to you. He smiled and said "Hi sweetie My name is Kisame just for you Kitty!"'

			act 'Say hello':
				cla
				'(You) - Hi! I <<$name>>.'
				'(Plavec) - <<$name>> can swim together?'
				'Kisame without waiting grabs you by the waist and powerful jump jumps to the middle of the pool.'

				act 'Next':gt 'kisameQW', 'start'
			end

			act 'Tell Kisame to fuck off':
				cla
				'(You) - Fuck off Kish!'
				'(Plavec) - How rude!'
				'You do not have time to blink as an athlete grabbed you by the waist and lifted over your head easily threw you into the middle of the pool.'
				'When you spitting surfaced over the water you can see how to you under the water at breakneck speed approaching Kisame.'

				act 'Next':gt 'kisameQW', 'start'
			end
		end
	elseif kisameQW = 1 and KisameDead < 1:
		'<center><img src="images/qwest/kisameV.jpg"></center>'
		'Seeing you akulopodobny Kisame smiling "Hi baby. Want me to fill your hot mouth with his shark-seed?"'

		act 'Attack Kisame':
			cla
			horny += 10
			$nameV = 'Kisame'
			!!stats
			strenV = 300
			speedV = 100
			agilV = 100
			reactV = 100
			vitalV = 200
			intelV = 100
			willV = 50
			!!skills
			magikV = 10
			boxingV = 100
			shootV = 100
			!!parameters
			healthV = vitalV * 10 + strenV * 5
			mannaV = intelV * magikV + magikV * 100 + vitalV * 10
			willpowerV = willV * 10
			BonusAtakV = 0
			BonusDefV = 0
			!!Programme of Action
			program = 5
			KisaFight = 1
			klonV = 5
			defenceV = 800

			if spellavtoklon = 1:klon = 3

			gt 'fight', 'start'
		end

		act 'Give him a blowjob under water':
			cla
			*clr
			cumspclnt = 10
			gs 'cum_cleanup'
			minut += 60
			horny += 100
			sweat = -3
			manna += 5
			willpower += 5

			if stren < 40:stren += 1
			if KisameSex = 0:KisameSex = 1 & Set guy += 1
			$boy = Kisame
			spafinloc = 12
			sexpartkno = 1
			gs 'Cum_Manage'
			bj += 1
			swallow += 1
			SUB += 1
			'<center><img src="images/qwest/kisameBJ.jpg"></center>'
			'You downcast eyes, Kisame smiled even wider and dragged you into the water. His strong hands pressed your face to his groin and you wrapped around his dick lips quite quickly Kisame finished your mouth.'

			act 'Out of the water':gt 'bass', 'start'
		end
	end

	act 'Swim (1:00)':
		cla
		*clr
		cumspclnt = 10
		gs 'cum_cleanup'
		minut += 60
		sweat = -3
		manna += 5
		willpower += 5

		if stren < 40:stren += 1

		'<center><img src="images/pics/swim.jpg"></center>'
		'You swim in the pool, the water refreshes and invigorates.'

		act 'Out of the water':gt 'bass', 'start'
	end

	act 'Leave':gt 'bass', 'start'
end

--- bass ---------------------------------

