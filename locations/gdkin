# gdkin
$din_Katja_dance ={

Act 'Dancing with Katya':
	minut += 15
	gs'stat'
	*clr & cla 
	dancerand=rand(1,5)
	if tanga=0: 
		'<center><img src="images/qwest/alter/katja/katja_dance1_2.jpg"></center>'  
		'You dance with Katya attracting views of men from across the room. And when you consider that you have no pants on, and you occasionally sparkle her pussy, the attention you provided. '
	else
		'<center><img src="images/qwest/alter/katja/katja_dance1_1.jpg"></center>'
		'You dance with Katya attracting views of men from across the room.'
	end
	act 'gambol':
		horny += 20 
		KatjaHorny += 20
		*clr & cla 
		if dancerand = 1: 
			'<center><img src="images/qwest/alter/katja/katja_dance2_2.jpg"></center>'
			'During the dance you have decided to fool around and embraced Katya, We showed peasants gathered around, Katina pussy.  What caused the approving roar around, and embarrassed squeak Katie.'
			act 'Dance on':
				*clr & cla
				'<center><img src="images/qwest/alter/katja/katja_dance2_1.jpg"></center>'
				'One of the guys gathered around you, unable to withstand such a picture, I jumped to you and you crawled under her skirt.'
				act 'Show language':
					*clr & cla
					'<center><img src="images/qwest/alter/katja/katja_dance1_3.jpg"></center>'
					'Rebuffing guy you turned to the crowd around you guys, Kate hugged and showed all gathered Language. And while the guys thought better not rushed lost among dancers.'
					act 'to get lost':  gt $curloc
				end
			end	
		elseif dancerand = 2:	
			'<center><img src="images/qwest/alter/katja/katja_dance3.jpg"></center>'
			'Dancing you can not control himself pulled Kate to her and put his hands on her chest, bit their breasts classmates. From Katie broke a light groan, and of boys gathered around quite hard...'
		elseif dancerand = 3:
			'<center><img src="images/qwest/alter/katja/katja_dance4.jpg"></center>'
			'Lighting Katya and moving quite actively, you do not notice how your breasts fell out from under clothes. But it is noticed guys standing around... '
		elseif dancerand = 4:
			'<center><img src="images/qwest/alter/katja/katja_dance5.jpg"></center>'
			'During the dance you can not resist, and under the cheers from the audience, climbed to Kate kiss...'
		elseif dancerand = 5:
			'<center><img src="images/qwest/alter/katja/katja_dance6.gif"></center>'
			'You dance with Katya paying little attention to what is happening,  then one of the guys around you with girlfriend. He reached out and pulled down your cleavage baring your breasts. Without ceasing to dance you have sent in its original state, a disgruntled cries of a circle around you...'
		end
		act 'Show language':
			*clr & cla
			'<center><img src="images/qwest/alter/katja/katja_dance1_3.jpg"></center>'
			'Looking at the crowd around you guys, you hugged Kate and showing tongue, They hurried to get lost among the dancers.'
			act 'to get lost':  gt $curloc
		end
	end
end
}
$din_kotov_love = {
	if kotovLoveQW > 0:
		cls
		gs 'stat'
		'<center><b><font color="maroon"><<$npcName[9]>> <<$npcSurName[9]>></font></b></center>'
		'<center><img src="images/qwest/alter/npc/9.jpg"></center>'
		'To you and the guy fits Witek. "Che garbage? Who''s that bloke?" Witek takes for male breast and angrily said, "Well, ka sebal horrified by my heifer, and now the mug smashed into smithereens. Understood?"'

		if DPgrup = 1:
			fightboyrand = rand(0, 10)

			if fightboyrand <= 5:
				'Frightened man began to speak. "Come on you, I did not know. Already leaving." Witek pushed Man "Get out of here until I''m good."'
			elseif fightboyrand > 5 and fightboyrand < 10:
				'The guy pushes Victor "Get your hands off, deer!" Witek suddenly gets very fast and powerful kick.'
				'Man falls from hitting the dance floor. Witek approaches him and grabbing breast lifts his feet, and then shoves the boy to the wall. "Well, Cho motherfucker? Still need? Or is it okay?" Man holding a broken person mutters "Everything, everything, I got it!"'
			elseif fightboyrand >= 10:
				'The guy has Vit''ka silence in the face with his fist. Witek recoils from the guy and then gets fast and powerful kick. Fight begins, hitting fly from both sides, suddenly a guy misses a very powerful supply by Vic and flying away to the wall falls to the floor. Witek runs up to him and thrusts into enemy lying a couple of powerful blows. The guy is no longer resisting, apparently he lost consciousness.'
			end
		elseif DPgrup = 2:
			'Rams kid starts with Vic "Hey, who are you in life?" Witek markedly sviripeet and to help him fit drugany. For the kid in turn, also pulled the boys. Nachinaetsya disassembly. After some time, both groups disagree peacefully.'
		elseif DPgrup = 3:
			'Man throws up his hands "Sorry young man. Did not know." Witek repels man "Topal city bloke here until I''m good."'
		end

		act 'Go to Witek':numnpc = 9 & gt 'Snpc'

		exit
	end
}

$din_reakt_dance = {
	if alko > 0:
		horny += 10
		manna += 5
		'Do you feel that the actions of Man excite you.'
	else
		if horny > 0:horny -= 5

		manna -= 5
		'You feel disgusted by these actions Man.'
	end

	gs 'stat'
}

$din_reverse_reakt_dance = {
	if alko <= 0:
		manna += 20
		horny += 1
		'You are fun and exciting.'
	elseif alko > 0:
		if horny > 0:horny -= 5

		manna -= 5
		'You boring and uninteresting.'
	end

	gs 'stat'
}

$din_dance_vrp = {
	minut += 5

	if dance < 50 and agil >=dance:dance += 1
	gs 'stat'
	*clr
	'<center><img src="images/etogame/club_dance5.jpg"></center>'
	'You accepted the offer and the guy took you by the hand brought you to the dance floor where you started dancing together.'

	if DPgrup = 1:
		if DPtipe = 1:
			'Guy dancing with you holding your waist with his hands.'
			dynamic $din_reverse_reakt_dance
		elseif DPtipe = 2:
			*clr &'<center><img src="images/etogame/club_dance6.jpg"></center>'
			'Drunk guy pushes you to indulge. You feel like your soft breasts literally flattened breasts of Man.'
			dynamic $din_reakt_dance
		end
	elseif DPgrup = 2:
		if DPtipe = 1:
			'The guy is pretty enough to unleash your waist and his hands slide your hips.'
			dynamic $din_reakt_dance
		elseif DPtipe = 2:
			*clr &'<center><img src="images/etogame/club_dance8.jpg"></center>'
			'Pyany guy grabs your ass and squeezes her much, his head tilted to your face and it bites into your <<$liptalk3>> kiss.'
			dynamic $din_reakt_dance
		end
	elseif DPgrup = 3:
		if DPtipe = 1:
			'Man very confident and elegantly leads you to dance, hold your waist with his strong hands.'
			dynamic $din_reverse_reakt_dance
		elseif DPtipe = 2:
			'Tipsy man despite his advanced views hugs your waist and his hands down to you on the ass.'
			dynamic $din_reakt_dance
		end
	end

	dynamic $din_vibor_tipe_dance

	act 'Stop dancing':
		cls
		gt $curloc
	end
}

$din_dance_otkaz = {
	if alko <= 2 and horny <= 75:
		act 'Refuse':
			cls
			! WD: Should be will not willpower
			will += rand(0, 1)
			gt 'gdkin'
		end
	end
}

$din_vibor_tipe_dance = {

	if dance >= 50 and alko > 0 and horny >= 50:
		act 'Sexy dance':
			cls
			'You are taking advantage of their ability to dance, sexy move forcing your partner to stare at you. But he seems stunned your courage, He believes that you can not handle it, and disappeared in the crowd.'
			'<center><img src="images/etogame/club_dance3.jpg"></center>'
			if tanga=0 and (ski = 1 or glamur = 1): *clr & '<center><img src="images/etogame/club_dance4.jpg"></center>' & 'Trying to move as much as possible sexier, you lean forward while doing a pirouette complex, completely forgetting that you no panties. Well now is your dance partner, It seems it will never forget. As long as you get better you skirt tightly surrounded wishing somebody else and your partner to see where it went missing.'
			dance += 1
			gs 'stat'
			act 'Further':gt'gdkin'

		end
	end


	act 'Further':
		cls
		gs 'stat'
		'<center><img src="images/etogame/club_dance.jpg"></center>'
		if tanga=0 and alco>0 and (ski = 1 or glamur = 1): *clr &'<center><img src="images/etogame/club_dance9.jpg"></center>'
		if alco>0: *clr &'<center><img src="images/etogame/club_dance10.jpg"></center>'

		if DPgrup = 1:
			if DPtipe = 1:
				'The guy told you in the dance "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." The song ends and the guy offers to sit with him and chat.'
			elseif DPtipe = 2:
				'The guy told you in the dance "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." All dancing drunk guy crushed your body and now his face flushed with excitement. He offers to leave the club on the street, get some fresh air, socialize.'
			end

			act 'Go with a guy':gt 'EvBoyBuh'
		elseif DPgrup = 2:
			if DPtipe = 1:
				'The entire dance guy grabs your ass and he is clearly excited when he pressed against you, you feel something hard in his pants. Finally zakanchivaetsya song and the guy offers to go out, plunk.'

				act 'Go thump':gt 'EvgopBuh'
			elseif DPtipe = 2:
				'The entire dance guy grabs your ass, breasts and kissed you, he clearly excited when he pressed against you, you feel something hard in his pants. Finally zakanchivaetsya song and the guy offers to go out, plunk.'

				act 'Go thump':gt 'EvgopBuh'
			end
		elseif DPgrup = 3:
			if DPtipe = 1:
				'All male dance was confident and elegant, finally the song was over and the man offers to go out for some fresh air and talk.'

				act 'Go with a man':
					cls
					gs 'stat'
					'<center><img src="images/etogame/dk_night.jpg"></center>'
					'You went out with a man. He walked up to you and bit his arm around her waist. "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." The man continued, "<<$name>>, I''m in your town on a business trip. Lives in the hotel from the city itself. Virtually nothing in your town do not know. Come to my hotel. Take whiskey, balls, sit down and have a chat."'

					act 'Go to the hotel':gt 'EvOtelMan'

					dynamic $din_dance_otkaz
				end
			elseif DPtipe = 2:
				'The entire dance tipsy man molested you when the dance ended and he whispered in your ear. "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." The man continued, "<<$name>>, I''m not a local, I live in a hotel, let''s now take a taxi and go to me."'

				act 'Go to the hotel':gt 'EvOtelMan'
			end
		end

		gs 'stat'
		dynamic $din_dance_otkaz
		dynamic $din_kotov_love
	end
}

$din_otbor_dance_pertner = {
	!!the creation of a partner to dance
	sozboyrand = rand(1, 300)
	if sozboyrand > boynum:
		gs 'BnameGen'
		DaPaTipeRand = rand(0, 100)

		if DaPaTipeRand < 70:
			!!ordinary local boy
			bmtimeTipe = 0
			DrunkDPTrand = rand(0, 100)

			if DrunkDPTrand < 70:
				DPtipe = 1
				DPgrup = 1
				$din_disc_dance = 'It suits you the guy in jeans and a t-shirt, and offers dance.'
				$din_disc_dance2 = 'Next to you dancing guy in jeans and a T-shirt.'
			elseif DrunkDPTrand >= 70:
				DPtipe = 2
				DPgrup = 1
				$din_disc_dance = 'It suits you drunk guy in jeans and a t-shirt, and offers dance.'
				$din_disc_dance2 = 'Next to you dancing drunk guy in jeans and a T-shirt.'
			end
		elseif DaPaTipeRand >= 70 and DaPaTipeRand < 90:
			!!local gopar
			bmtimeTipe = 1
			DrunkDPTrand = rand(0, 100)

			if DrunkDPTrand < 50:
				DPtipe = 1
				DPgrup = 2
				$din_disc_dance = 'It suits you guy dressed in sweatpants and a sweatshirt and the same offers to dance.'
				$din_disc_dance2 = 'Next to you dancing guy in sweatpants and sweatshirts.'
			elseif DrunkDPTrand >= 50:
				DPtipe = 2
				DPgrup = 2
				$din_disc_dance = 'It suits you drunk guy dressed in sweatpants and sweatshirts same and takes you arms around his waist offering dance.'
				$din_disc_dance2 = 'Next to you dancing drunk guy in sweatpants and sweatshirts.'
			end
		elseif DaPaTipeRand >= 90:
			!!traveling_expenses
			bmtimeTipe = 2
			DrunkDPTrand = rand(0, 100)

			if DrunkDPTrand < 90:
				DPtipe = 1
				DPgrup = 3
				$din_disc_dance = 'To you fit man in a decent jacket, trousers with arrows, expensive shoes, and offers dance.'
				$din_disc_dance2 = 'Next to you dancing man in a decent jacket, trousers with arrows, expensive shoes.'
			elseif DrunkDPTrand >= 90:
				DPtipe = 2
				DPgrup = 3
				$din_disc_dance = 'It suits you drunk man in a decent jacket, trousers with arrows, expensive shoes, and offers dance.'
				$din_disc_dance2 = 'Near you drunk man dancing in a decent jacket, trousers with arrows, expensive shoes.'
			end
		end
		gs 'boymass'
	elseif sozboyrand <= boynum:
		Tboynum = sozboyrand

		if bmTip[Tboynum] = 0:
			$niknameboy = 'Guy'
			rudeboy = 1
			$text = '<center><img src="images/etogame/club_dance_gay.jpg"></center> It suits you the guy in the jeans and T-shirt, and invites you to dance. Appearance guy seems familiar to you, apparently you have to dance with him.'
		elseif bmTip[Tboynum] = 1:
			$niknameboy = 'Gopnik'
			rudeboy = 2
			$text = '<center><img src="images/etogame/club_dance_gop.jpg"></center> It suits you man dressed in sweatpants and sweatshirts and the same offers to dance. Appearance guy seems familiar to you, apparently you have to dance with him.'
		elseif bmTip[Tboynum] = 2:
			$niknameboy = 'Man'
			rudeboy = 0
			$text = '<center><img src="images/etogame/club_dance_man.jpg"></center> It is right for you in a decent man jacket, pants with arrows, expensive shoes, and invites you to dance. Outside the men seem familiar to you, apparently you have to dance with him.'
		end

		gt 'BMloc'
	end
}

$din_too_drunk_dance = {
	if StoryLine = 1 and BeInSchool > 0:
		cls
		gs 'stat'
		'<center><img src="images/etogame/club_dance_alco1.jpg"></center>'
		'It suits you Katja Meynold "<<$name>>, so that you get drunk? Let''s go home, I''ll take you."'

		act 'Go with Katja':
			cls
			minut += 15
			grupNPC[14] = 100

			if meynoldQW = 0:meynoldQW = 1

			gs 'stat'
			'<center><img src="images/etogame/club_dance.jpg"></center>'
			'Katja took you to your home keeping the arms.'

			act 'Go home':gt 'korrPar'
		end

		exit
	else
		gt 'gdkin'
	end
}

$metka = $ARGS[0]
$loc = $CURLOC
boynumBlock = 0
gs 'stat'
fcolor = rgb(0, 0, 0)
bcolor = rgb(255, 255, 255)
lcolor = rgb(106, 90, 205)

pl ''

if music_off = 0:
	inited = 1
	music_on = 1
	volume = 100
	$music_theme = 'gdis'
	gs 'init_music'
	pl '<a href="exec: music_off = 1 & gt $loc">Turn off the music</a>'
else
	music_on = 0
	volume = 100
	gs 'init_music'
	pl '<a href="exec: music_off = 0 & gt $loc">Play music</a>'
end

'<center><b><font color="maroon">Disco</font></b></center>'
'<center><img src="images/etogame/club_yaho.jpg"></center>'

if hour >= 20 and hour <= 23:
	if StoryLine = 1 and BeInSchool > 0:'The club and you see a few <a href="exec:gt ''SgrupDisco''">of people you know</a>.'
	if soniaslut < 5:soniaslut += 1

	if grupTipe[25] = 0 and StoryLine = 1 and BeInSchool > 0 and soniaslut >= 5 and hour < 23:
		'You notice the disco Sonia Ivanko of your class. She is talking about something with Victor Kotov.'

		act 'View Sonya':gt 'soniaDisco'

		exit
	end

	!'
	if soniaPS >= 1 and hour <= 21 and meynoldQW = 0 and BeInSchool > 0:
		''
		exit
	end
	'!

	act 'Dance one':
		cls
		'<center><img src="images/etogame/club_dance.jpg"></center>'
		minut += 5
		!!fat -= 1

		if dance < 50 and agil >= dance:dance += 1

		manna += 10
		willpower += 10 & ! WD: Should be willpower as is mood
		frost = 0
		gs 'stat'

		if spdirtyc > 0 or spdirtyv > 0:
			if GorSlut < 2:GorSlut = 2

			manna -= 100
			'It suits you and the guy starts to stick, suddenly he sees you cum. The guy jumps on you "thanked, vaflersha. Lads estimate, this chick all in Concha peremazanna." To you guys still see fit. "Yeah sure. Hey nipple unsuccessfully vysmarkalas what?" You cheeks blush with shame and you''re heading for the exit.'

			act 'Leave':gt 'gdk'

			exit
		end
		if rand(0,7)=1 and katja_disco_end=1 and (alko > 1 or horny => 80) and KatjaHorny > 60:
			*CLR
			'<center><img src="images/qwest/alter/katja/katja_dance.jpg"></center>'
			'While you are dancing around you start to curl Katia.'
			dynamic $din_Katja_dance
		END

		if alko >= 4:
			'You''re drunk in the trash and try to dance, but the floor out from under my feet and you dance instead of just drunkenly Mota regularly bumping into other people.'
			if rand (0,1) = 0:dynamic $din_too_drunk_dance & exit
		elseif alko > 0:
			*clr 
			'<center><img src="images/etogame/club_dance7.jpg"></center>'
			'You''re drunk and fun skip on the dance floor without paying attention to others, and even the music.'
			if tanga=0 and (ski = 1 or glamur = 1):  *clr & '<center><img src="images/etogame/club_dance2.gif"></center>' & 'You `re drunk , and you do not have on panties, and you do not try, but under the skirt and then shows your pussy. Blazing the whole club.'
		else
			'The music is quite old, but catchy. And you''ll have fun circling dance, paying no attention to anyone.'
		end

		if alko < 4:
			verprigrand = rand(0, 100)

			if verprigrand <= dance:
				dynamic $din_otbor_dance_pertner
				gs 'stat'
				'<<$din_disc_dance2>>'

				act 'Dance with a guy':
					cls
					'<center><img src="images/etogame/club_dance.jpg"></center>'
					dynamic $din_dance_vrp
				end

				act 'Move away':
					cls
					gt $curloc
				end

				exit
			end
		end

		gs 'stat'

		act 'Further':gt 'gdkin'
	end

	act 'Standing at the wall':
		cls
		minut += 5
		!!fat -= 1
		manna -= 5
		frost = 0
		gs 'stat'
		'<center><img src="images/etogame/club_yaho.jpg"></center>'

		if spdirtyc > 0 or spdirtyv > 0:
			if GorSlut < 2:GorSlut = 2

			manna -= 100
			'It suits you and the guy starts to stick, suddenly he sees you cum. The guy jumps on you "thanked, vaflersha. Lads estimate, this chick all in Concha peremazanna." To you guys still see fit. "Yeah sure. Hey nipple unsuccessfully vysmarkalas what?" You cheeks blush with shame and you''re heading for the exit.'

			act 'Leave':gt 'gdk'

			exit
		end
	
		if alko >= 6 and rand (1, 100) < 3:
			gt 'gevent', '6'
		end

		if alko >= 4:
			'You''re drunk in the trash, and standing at the wall hard for her to restrain from the fact that the floor floats you from under their feet.'
			if rand (0,1) = 0:dynamic $din_too_drunk_dance & exit

		elseif alko > 0:
			'You''re drunk, and standing at the wall watching the dancing.'
		else
			'You are standing at a modest stenochki watching dancing.'
		end

		if alko < 4:
			verodance = 0

			if hapri = 1:verodance += 1
			if glamur = 1:verodance += 2
			if ski = 1:verodance += 1
			if mop = 2:verodance += 1
			if mop = 3:verodance += 2
			if mop = 4:verodance += 3

			verodance += alko
			verodance += 5
			verprigrand = rand(0, 20)

			if verprigrand <= verodance:
				dynamic $din_otbor_dance_pertner
				gs 'stat'
				'<<$din_disc_dance>>'

				act 'Dance':
					cls
					gs 'stat'
					'<center><img src="images/etogame/club_dance.jpg"></center>'
					dynamic $din_dance_vrp
				end

				act 'Refuse':
					cls
					gt $curloc
				end

				exit
			end
		end

		gs 'stat'

		act 'Further':gt 'gdkin'
	end
elseif hour < 20:
	'Club closes. The lights go on, the music fades and all begin to diverge.'
end

act 'Go outside':gt 'gdk'

--- gdkin ---------------------------------

