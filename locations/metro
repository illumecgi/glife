# metro
if $ARGS[0] = 'start':
	CLOSE ALL
	if sound = 0:PLAY 'sound/metro.mp3',30
	'<center><b><font color="maroon">Metro station</font></b></center>'
	if $loc = 'street':'<center><b><font color="maroon">Residential area</font></b></center>'
	if $loc = 'down':'<center><b><font color="maroon">Center</font></b></center>'
	if $loc = 'Nord':'<center><b><font color="maroon">Northern region</font></b></center>'

	'<center><img src="images/etogame/metro.jpg"></center>'
	'Subway fare costs 16 rubles.'
	minut += 15
	clr
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)

	if workDisk = 3 and konvert = 1:
		badrand = rand(0, 100)
		if badrand >= 80:konvert = 0
	end

	if money >= 16:
		if $loc ! 'street':
			act 'Go to the <b>residential area</b> (16 rubles)':
				$loc = 'street'
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'down':
			act 'Go to the <b>center</b> (16 rubles)':
				$loc = 'down'
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'Nord':
			act 'Go to the <b>northern area</b> (16 rubles)':
				$loc = 'Nord'
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'liam':
			act 'Go to the <b>the old town</b> (16 rubles)':
				$loc = 'liam'
				gt 'metro', 'vagon'
			end
		end

	end

	act 'Get off the subway':
		minut += 15
		gt '<<$loc>>'
	end
end

if $ARGS[0] = 'vagon':
	cla
	clr
	CLOSE ALL
	if sound = 0:PLAY 'sound/metro1.mp3',30
	money -= 16
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Subway car</font></b></center>'
	if hour < 5 or hour > 10 and hour < 15 or hour > 24:
		'<center><img src="images/etogame/vagon.jpg"></center>'
		'In the car are just a few passengers vacantly reading magazines and papers.'
	elseif hour >= 5 and hour <= 10 or hour >= 15 and hour <= 24:
		'<center><img src="images/etogame/vagonFull.jpg"></center>'
		'In the car there are lots of people all standing like statues, trying not to make eye conteact with each other.'
		frotrand = rand(0, 100)

		if frotrand >= 90 - (frot * 10) and frot < 3:
			gt 'frot', 'a'
		elseif frotrand >= 90 - (frot * 10) and frot >= 3 and spdirtyc = 0 and ski = 1 or frotrand >= 90 - (frot * 10) and frot >= 3 and spdirtyc = 0 and glamur = 1:
			tipfrot = rand(0, 1)

			if tipfrot = 0:gt 'frot', 'b'
			if tipfrot = 1:gt 'frot', 'd1'

			!!gt 'frot', 'b'
		elseif frotrand >= 90 - (frot * 10) and frot >= 3 and spdirtyc = 0 and ski = 0 or frotrand >= 90 - (frot * 10) and frot >= 3 and spdirtyc = 0 and glamur = 0:
			tipfrot = rand(0, 2)

			if tipfrot =0:gt 'frot', 'c'
			if tipfrot =1:gt 'frot', 'd'
			if tipfrot =2:gt 'frot', 'e'
		end
	end

	if sosedQW >= 1:
		if hour = 7 or hour = 15:
			sosedMetroRand = rand(0, 100)

			if sosedMetroRand >= 60 and sosedMetroDay ! daystart:'You notice in the subway <a href="exec:gt ''TimofeiMetro''">Timothy</a>, your neighbor.'
		end
	end

	if spdirtyc > 0 or spdirtyv > 0:
		if hour >= 7 and hour <= 21:
			manna -= 10
			'Several passengers are shocked, looking at where you are cum smeared.'
		end
	end

	act 'Get out of the car':gt 'metro', 'start'
end

--- metro ---------------------------------

