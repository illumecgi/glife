# stwork
if $ARGS[0] = 'start':
	$stripgirl = {
		cls
		gs 'stat'
		striptalkrand = rand(0, 10)

		if striptalkrand = 0:$striptalk = 'Have you heard? There''s this new girl, all beautiful and everything, but on stage she is a complete zero, can not do anything, all she earned was boos.'
		if striptalkrand = 1:$striptalk = 'I''m telling you, men give birth to girls with tattoos.'
		if striptalkrand = 2:$striptalk = 'Working the pole is much more profitable than just dance striptease, easier to excite the customer, but it''s hard work.'
		if striptalkrand = 3:$striptalk = 'Phew, you really have to leave it all out on the stage, getting back all sweaty, disheveled hair and makeup ruined. The cost in cosmetics is enough to bankrupt you.'
		if striptalkrand = 4:$striptalk = 'You know what, those private dances might be guaranteed money, but the real cash is in the extras.'
		if striptalkrand = 5:$striptalk = 'Remember that ugly girl? Got on stage and immediately the crowd boo, but as soon as she started spinning around the pole, the customers jaws dropped, and she made a fortune in tips.'
		if striptalkrand = 6:$striptalk = 'Why doesn''t the manager ever says if it''s a male or female day? I recently rushed to work, even broke a nail, then I get here and it''s these bears up on stage. Had to turn around and go back.'
		if striptalkrand = 7:$striptalk = 'That cute security guard was telling me about the problems with this job. Half of the strippers end up whores.'
		if striptalkrand = 8:$striptalk = 'There was a fat woman trying to dance on the pole, she fell face first on the floor. I thought she had knocked herself out, you should have seen the bruise.'
		if striptalkrand = 9:$striptalk = 'Recently one joker raised a laugh in the hall. Pretty little thing and dances well, but that''s not how you earn tips here.'
		if striptalkrand = 10:$striptalk = 'I heard about these clubs abroad where the tips are so good, that the strippers pay for the right to work there.'

		'The girls are doing their makeup and hair in the mirror, laughing and gossiping.'
		'<<$striptalk>>'

		act 'Move away':gt $curloc, 'start'
	}

	$stripohr = {
		cls
		gs 'stat'
		'A bored looking, slab of a guard stands by the door. Seeing you he lazily nods, "Hello <<$name>>."'

		act 'Move away':gt $curloc, 'start'
	}

	$striptoilet2 = {
		cls
		gs 'stat'
		'You rinsed the razor blade which looks good enough for <<stanok>> more uses.'

		if leghair > 0 and stanok > 0:
			act 'Shave your legs (0:15)':
				cla
				*clr
				minut += 15
				leghair = -5
				horny += 5
				stanok -= 1
				'<center><img src="images/pics/shave.jpg"></center>'
				'You lather up and shave the hair from your legs.'

				act 'Rinse':dynamic $striptoilet2
			end
		end

		if lobok > 0 and stanok > 0:
			act 'Shave pubis (0:15)':
				cla
				*clr
				minut += 15
				lobok = -5
				horny += 5
				stanok -= 1
				'<center><img src="images/pics/shave3.jpg"></center>'
				'You lather your pubis, then shave all the hair from it.'

				act 'Rinse':dynamic $striptoilet2
			end
		end

		act 'Exit shower and get dressed':dynamic $striptoilet
	}

	$striptoilet = {
		cls
		gs 'stat'
		'<center><img src="images/LakeCafe/Toilet.jpg"></center>'
		'Dirty and smelly toilet that seems to have never been cleaned'

		if tanga = 1:
			act 'Remove Underwear':
				cla
				tanga = 0
				dynamic $striptoilet
			end
		end

		act 'Look in the mirror':
			cla
			gs 'portmirror'
			$gt1 = 'stwork'
			$gt2 = ''
			dynamic $portmirror

			act 'Move away from the mirror':
				cla
				dynamic $striptoilet
			end
		end

		if mop ! 1:
			act 'Wash off makeup':
				cla
				*clr
				minut += 5
				mop = 1
				'<center><img src="images/etogame/mop.jpg"></center>'
				'You carefully wash off your makeup.'

				act 'Leave':dynamic $striptoilet
			end
		end

		act 'Take a shower (0:15)':
			cla
			*clr
			minut += 15
			sweat = -3
			horny += 1
			hapri = 0
			mop = 1
			cumspclnt = 1
			gs 'cum_cleanup'
			'<center><img src="images/pics/dush.jpg"></center>'
			'You get into the cubicle and turn on the shower. Then lather up your body and wash in the shower.'

			if leghair > 0 and stanok > 0 or lobok > 0 and stanok > 0:
				act 'Get razor':dynamic $striptoilet2
			end

			if horny >= 90:
				act 'Use shower on your pussy':
					cla
					*clr
					horny = 0
					minut += 30
					mastr += 1
					orgasm += 1
					manna += 15
					willpower += 15
					'<center><img src="images/pics/wash2.jpg"></center>'
					'You point the shower hose at your pussy and a tight jet of warm water begins to caress you. Soon you feel a warm wave cover your entire body and you begin to shudder in orgasm.'

					act 'Get out and get dressed':dynamic $striptoilet
				end
			elseif horny >= 40 and horny < 90:
				act 'Use shower on your pussy':
					cla
					*clr
					horny += 5
					minut += 30
					manna += 10
					willpower += 10
					'<center><img src="images/pics/wash2.jpg"></center>'
					'You point the shower hose at your pussy and a tight jet of warm water begins to caress you. At first you feel very nice, but after a while, it starts to get a little painful. You stop masturbating.'

					act 'Get out and get dressed':dynamic $striptoilet
				end
			end

			act 'Exit shower and get dressed':dynamic $striptoilet
		end

		!!0.2.6 extension: reworked how quick wash and face wash was supposed to work, it is now uniform in all toilet/bathroom locations
		!!Removed sweat =- 1 when cum is wiped off clothes
		!!clrbelo: Amount of clean panties available
		if spdirtyi > 0 or spdirtyn > 0:
			act 'Quick wash (0:15)':
				cla
				*clr

				if clrbelo > 0 and tanga = 1:
					clrbelo -= 1
					dirtbelo += 1
				elseif clrbelo = 0 and tanga = 1:
					tanga = 0
					dirtbelo += 1
				end

				dynamic $showerdin
				sweat -= 1
				minut += 15
				'<center><img src="images/pics/wash5.jpg"></center>'
				'You gave yourself a catlick, thus removing any traces of cum.'

				act 'Finish':dynamic $striptoilet
			end
		end

		temp = arrpos('sparrloc',11)
		if sparrloc[temp] = 11 and sparrvol[temp] > 0:
			temp = 1
		else
			temp = arrpos('sparrloc',16)
			if sparrloc[temp] = 16 and sparrvol[temp] > 0:
				temp = 1
			else
				temp = 0
			end
		end
		if temp > 0 or spdirtyb > 0 : 
			act 'Wash cum off face (0:15)':
				cla
				*clr
				mop = 1
				cumspclnt = 13
				gs 'cum_cleanup'
				sweat -= 1
				minut += 15
				'<center><img src="images/etogame/facesp.jpg"></center>'
				'You clean your face and get rid of any trace of cum.'
				act 'Finish':dynamic $striptoilet
			end
		end

		if spdirtyc > 0:
			act 'Wipe the cum off your clothes (0:10)':
				cla
				*clr
				cumspclnt = 6
				gs 'cum_cleanup'
				minut += 10
				!!'<center><img src="images/etogame/frotsp.jpg"></center>'
				'You remove traces of cum from your clothes.'

				act 'Finish':dynamic $striptoilet
			end
		end
		!!

		act 'Leave the toilet':gt $curloc, 'start'
	}
	$sexloc = $CURLOC
	$metkaM = $ARGS[0]
	$locM = $CURLOC
	clr
	minut += 5
	stripMir = 1
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Dressing room for strippers</font></b></center>'
	'<center><img src="images/etogame/striprazd.jpg"></center>'
	'The walls are lined with <a href="exec:gt ''mirror'', ''start''">mirrors</a>. There are steps leading to the <a href="exec:dynamic $stripgirl">stage</a> where you and the girls perform.'
	'To the side is a <a href="exec:stripMir = 0 & gt ''stripclub'', ''start''">steel door</a> which overlooks the back yard and all the staff use as an entrance, to save them going through the front door. By this entrance is always a <a href="exec:dynamic $stripohr">guard</a>.'
	'Behind a wooden door is the <a href="exec:dynamic $striptoilet">staff toilets</a>.'
	if tatplech > 0:stripKoef = rand(1, 3)
	if tatback > 0:stripKoef2 = rand(1, 3)
	if tatback > 0:stripKoef3 = rand(1, 3)
	if tatupb > 0:stripKoef4 = rand(3, 6)
	if tatleg > 0:stripKoef5 = rand(1, 3)

	publichot = vnesh - 10 + stripKoef + stripKoef2 + stripKoef3 + stripKoef4 + stripKoef5
	publichotstart = publichot

	if hour >= 15 or hour <= 2:
		if dirtyn <= 0 and dirtyv <= 0:
			if mop >= 4:
				if hapri = 1:
					if sweat <= 0:
						if leghair <= 0 and lobok <= 0:
							if fingal <= 0:
								act 'Go on stage':stpredmet = 3 & gt 'stwork2'
							else
								'With such a black eye as you have, you aren''t allowed on stage.'
							end
						else
							'There is nothing more terrible than a stripper with hairy legs or pubis, have a shave before you can work.'
						end
					else
						'Damn, you stink like a horse, have a wash first.'
					end
				else
					'The least you can do is brush your hair.'
				end
			else
				'You need to very thick makeup if you want people to see it in this light.'
			end
		else
			'There is visible semen on your body. Wash yourself off before going on stage.'
		end
	else
		'That''s it for today. Time to go home.'
	end
end

--- stwork ---------------------------------

