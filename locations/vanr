# vanr
CLOSE ALL

$metka = $ARGS[0]
$loc = $CURLOC
$metkaM = $ARGS[0]
$locM = $CURLOC
act 'Go to the corridor':gt 'korr'

$stirkad = {
	cla
	*clr
	minut += dirtbelo * 5
	suhbelo += dirtbelo
	poroshok -= 1
	dirtbelo = 0
	manna -= 10
	'<center><img src="images/etogame/stir.jpg"></center>'
	'You soak the dirty linen in a basin and pour the powder on them. You wash the clothes until they are finally clean.'

	act 'Hang the laundry out to dry':gt 'korr'
}

clr
minut += 1
housrA = 1
elektro += 1
gs 'stat'
fcolor = rgb(0, 0, 0)
bcolor = rgb(255, 255, 255)
lcolor = rgb(106, 90, 205)
'<center><b><font color="maroon">Bathroom</font></b></center>'

if remvanr = 0:'<center><img src="images/etogame/vanr.jpg"></center>'
if remvanr = 1:'<center><img src="images/etogame/vanr2.jpg"></center>'

if stiralka > 0:
	if husband > 0 and husbanday > 0 and husporday ! daystart and poroshok >= 2:
		husband += 5
		poroshok -= 2
		husporday = daystart
		'Washing machine washed smelly socks husband and his prossannye and properdannye truselya.'
	end

	if dirtbelo > 0 and poroshok >= dirtbelo:
		clrbelo += dirtbelo
		poroshok -= dirtbelo
		dirtbelo = 0
		'The washing machine washed your laundry'
	elseif poroshok < dirtbelo:
		'You ran out of powder for washing machine.'
	end
end


if tampon > 0:
	'A box of tampons is on a shelf. There is enough for <b><<tampon>></b> uses'
else
	'<b><font color="red">You have no tampons.</font></b>'
end

if stanok > 0:'A package of razors is on a shelf above the sink. You have enough blades for <b><<stanok>></b> shaves.'
if dirtbelo>0:'The laundry basket currently has <font color="red"><b><<dirtbelo>></b></font> pieces of dirty laundry.'
if poroshok > 0:
	if dirtbelo = 0:$stirka = ''
	if dirtbelo > 0:$stirka = '<a href="exec:dynamic $stirkad">You can wash clothes</a>'
	'Under the sink there''s detergent powder, which is enough for <b><<poroshok>></b> washes. <<$stirka>>'
else
	'<b><font color="red">You have no detergent powder and nothing to wash clothes with.</font></b>'
end

if clrbelo > 0:
	'A cupboard contains <font color="blue"><b><<clrbelo>></b></font> pairs of clean underwear. '
else
	'<b><font color="red">You do not have any clean underwear.</font></b> '
end

if shampoo > 0:
	'You have enough shampoo for <b><<shampoo>></b> washes.'
	dynamic $shower
	dynamic $bath
else
	'You''ve run out of shampoo and will have to get some more before you can wash yourself.'
end

if pirsA + pirsB + pirsC + pirsD + pirsE + pirsF + pirsN + pirsG + pirsGL > 0:'<a href="exec:dynamic $pirManage">Manage Piercings</a>'

if fillimplant = 1 and siliconeBag >= 1:'<a href="exec:cls & siliconeBag -= 1 & silicone += 10 & gt $curloc">Inject silicone into breast implants.</a>'
if fillimplant = 1 and silicone >= 20:'<a href="exec:cls & silicone -= 10 & gt $curloc">Drain silicone from breast implants.</a>'

if vanr_hanger = 1: 'Next to the sink neatly folded your <a href="exec:dynamic $vanr_hanger_dress & GT ''vanr'' ">clothes</a>'


if mosolmaz>0:'On a shelf is a tube of ointment for chafing, which will be enough for <b><<mosolmaz>><b> applications.'
if stiralka > 0:'In the bathroom is a washing machine.'

dynamic $bteeth
dynamic $tampon
dynamic $quickwash
dynamic $wearpan
dynamic $basin

if klisma = 1 and klismaday ! daystart:
	act 'Give yourself an enema (0:05)':
		cla
		*clr
		cumspclnt = 7
		gs 'cum_cleanup'
		minut += 5
		klismaday = daystart
		klismaday1= 1
		'<center><img src="images/picV/klisma.jpg"></center>'
		'You give yourself an enema and clean your intestines. Your ass is now ready for anal sex.'

		act 'Finish':gt $curloc
	end
end

if vibrator = 1 and vibratorIN = 1:
	act 'Remove the vibrator (0:05)':
		cla
		*clr
		minut += 5
		vibratorIN = 0

		if vagina < 30:vagina += 1

		'<center><img src="images/etogame/vibrator.jpg"></center>'
		'You bend over the sink and take the vibrator out of your pussy.'

		act 'Finish':gt 'vanr'
	end
elseif vibrator = 1 and vibratorIN = 0:
	act 'Insert the vibrator in your vagina (0:05)':
		cla
		*clr
		minut += 5
		vibratorIN = 1

		if vagina < 30:vagina += 2

		'<center><img src="images/etogame/vibrator.jpg"></center>'
		'You bend over the sink and inserted the vibrator in your pussy.'

		act 'Finish':gt 'vanr'
	end
end
if analplug = 1 and analplugin = 1:
	act 'Remove the butt plug (0:05)':
		cla
		*clr
		minut += 5
		analplugIN = 0

		if anus < 30:anus += 1

		'<center><img src="images/body/analplug.jpg"></center>'

		'You bent over and took out the butt plug.' & !!0.2.6: Removed unneccessary var check

		act 'Finish':gt $curloc
	end
elseif analplug = 1 and analplugin = 0 and klismaday = daystart:
	act 'Insert a butt plug (0:05)':
		cla
		*clr
		minut += 5
		analplugIN = 1

		if anus < 10:anus = anus + 5
		if anus >= 10 and anus < 20:anus += rand(1, 2)
		if anus >= 20 and anus < 30:anus += rand(0, 1)

		'<center><img src="images/body/analplug.jpg"></center>'

		if butpluguse > 0:'You bent over and inserted the butt plug.'
		if butpluguse = 0:butpluguse = 1 & '"If I don''t use it then why did I buy it!" you thought. You decided to take a closer look while thinking, "It is necessary to start developing my ass so I could have great adventures someday, you could even say a huge and thick adventures with crimson heads, uh, now I got carried away." You scratched your head. Then you directed it in your ass, it was a bit painful at first, but as soon as the widest part was plunged into the murky depths of your anus it became easier to push it all the way.'

		act 'Leave':gt $curloc
	end
end

! WD: add missing ointment
if mosolmaz > 0 and mosol > 0:
	act 'Rub ointment onto your pussy (0:05)':
		cla
		minut += 5
		mosolmaz -= 1
		mosol -= 5
		'You take a tube of cream and rub it onto your itchy vulva.'

		act 'Leave':gt $curloc
	end
end

if husband > 0 and husbanday > 0 and husporday ! daystart and poroshok >= 2:
	act 'Wash your husbands clothes (2 units of washing powder) (1:00)':
		cla
		*clr
		husband += 5
		poroshok -= 2
		minut += 60
		husporday = daystart
		manna -= 25
		'<center><img src="images/etogame/stir.jpg"></center>'
		'You spend an hour washing your husbands smelly socks and his prossannye and properdannye truselya.'

		act 'Move away from the bathroom':gt 'vanr'
	end
end

if husband > 0 and husbanday > 0 and huspolday ! daystart:
	act 'Clean the apartment (1:00)':
		cla
		*clr
		husband += 5
		minut += 60
		huspolday = daystart
		manna -= 25
		!!'<center><img src="images/pics/cook.jpg"></center>'
		'You take a rag and start cleaning the apartment.'

		act 'Leave':gt 'vanr'
	end
end

if pregtest > 0:
	act 'Do a pregnancy test':
		cla
		pregtest -= 1

		gs 'stat'

		if preg = 1:
			knowpreg = 1
			'The test showed the two strips. you are pregnant.'
			!!'father - <<$father>>.'
		else
			'The test showed one band.'
		end

		act 'Further':gt $curloc
	end
end

--- vanr ---------------------------------

